# Maintainer: Dudemanguy <dudemanguy@artixlinux.org>
pkgname=s6-scripts
pkgver=20210506
pkgrel=1
pkgdesc='A collection of essential s6-rc oneshots and longruns for startup/shutdown.'
arch=('any')
url='https://gitea.artixlinux.org/artix/s6-scripts'
provides=('init-udev' 'eudev-s6')
depends=('bash' 'execline' 'pam' 's6-rc')
replaces=('eudev-s6')
optdepends=('cryptsetup-s6: cryptsetup boot script support'
            'lvm2-s6: lvm2 boot script support')
backup=('etc/s6/rc.local'
        'etc/s6/s6.conf'
        'etc/s6/sv/agetty-serial/conf'
        'etc/s6/sv/agetty-tty1/conf'
        'etc/s6/sv/agetty-tty2/conf'
        'etc/s6/sv/agetty-tty3/conf'
        'etc/s6/sv/agetty-tty4/conf'
        'etc/s6/sv/agetty-tty5/conf'
        'etc/s6/sv/agetty-tty6/conf'
        'etc/s6/sv/dmesg/conf'
        'etc/s6/sv/mount-net/dependencies'
        'etc/s6/sv/rc-local/dependencies'
        'etc/s6/sv/udevd-log/conf')
install=s6-scripts.install
makedepends=('git')
_commit=589a855f44b4a7c7d734d8c1cb3ff83131baa74d
source=("git+https://gitea.artixlinux.org/artix/s6-scripts.git#commit=$_commit")
sha256sums=('SKIP')

_inst_sv(){
    cd $1
    for file in conf consumer-for contents dependencies down notification-fd \
                pipeline-name producer-for run shell_down shell_up type up; do
        if test -f "$srcdir/s6-scripts/$1/$file"; then
            install -Dm644 "$srcdir/s6-scripts/$1/$file" "$pkgdir/etc/s6/sv/$1/$file"
        fi
    done
    for file in shell_up; do
        if test -f "$pkgdir/etc/s6/sv/$1/$file"; then
            chmod +x "$pkgdir/etc/s6/sv/$1/$file"
        fi
	done
    cd ..
}

package() {
    cd ${pkgname}

    # various installs
    install -Dm644 "${srcdir}"/s6-scripts/50-default.conf "${pkgdir}"/usr/lib/sysctl.d/50-default.conf
    install -Dm644 "${srcdir}"/s6-scripts/rc.local "${pkgdir}"/etc/s6/rc.local
    install -Dm644 "${srcdir}"/s6-scripts/s6.conf "${pkgdir}"/etc/s6/s6.conf
    install -Dm755 "${srcdir}"/s6-scripts/modules-load "${pkgdir}"/usr/bin/modules-load 
    install -Dm755 "${srcdir}"/s6-scripts/mount-cgroups/cgroup-release-agent.sh "${pkgdir}"/etc/s6/sv/mount-cgroups/cgroup-release-agent.sh
    install -d "${pkgdir}"/usr/share/man/man8
    install -Dm644 "${srcdir}"/s6-scripts/modules-load.8 "${pkgdir}"/usr/share/man/man8

    # temporarily leave out the mount-tmpfs dependency for now to avoid automatically overwriting a user's current /tmp dir
    sed -i '/mount-tmpfs/d' $srcdir/s6-scripts/mount-filesystems/dependencies

    # install startup/shutdown scripts
    _inst_sv 'agetty-serial'
    _inst_sv 'agetty-tty1'
    _inst_sv 'agetty-tty2'
    _inst_sv 'agetty-tty3'
    _inst_sv 'agetty-tty4'
    _inst_sv 'agetty-tty5'
    _inst_sv 'agetty-tty6'
    _inst_sv 'binfmt'
    _inst_sv 'boot'
    _inst_sv 'cleanup'
    _inst_sv 'console-setup'
    _inst_sv 'default'
    _inst_sv 'dmesg'
    _inst_sv 'early-getty-down'
    _inst_sv 'getty'
    _inst_sv 'hostname'
    _inst_sv 'hwclock'
    _inst_sv 'kmod-static-nodes'
    _inst_sv 'locale'
    _inst_sv 'misc'
    _inst_sv 'modules'
    _inst_sv 'mount'
    _inst_sv 'mount-cgroups'
    _inst_sv 'mount-devfs'
    _inst_sv 'mount-filesystems'
    _inst_sv 'mount-net'
    _inst_sv 'mount-procfs'
    _inst_sv 'mount-sysfs'
    _inst_sv 'mount-tmpfs'
    _inst_sv 'net-lo'
    _inst_sv 'random-seed'
    _inst_sv 'rc-local'
    _inst_sv 'remount-root'
    _inst_sv 'setup'
    _inst_sv 'swap'
    _inst_sv 'sysctl'
    _inst_sv 'sysusers'
    _inst_sv 'tmpfiles-dev'
    _inst_sv 'tmpfiles-setup'
    _inst_sv 'udev'
    _inst_sv 'udevadm'
    _inst_sv 'udevd-log'
    _inst_sv 'udevd-srv'
}
